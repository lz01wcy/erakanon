@EVENTEND
#LATER
PRINTL 調教を終了しました。

;調教後行為のチェック
CALL SELF_CHECK

;何の珠を得られたか
CALL JUEL_CHECK

;否定の珠の打ち消しチェック
CALL DENIAL_CHECK

BEGIN ABLUP

@JUEL_CHECK
REPEAT 14
	IF PALAM:COUNT < PALAMLV:1
		G = 0
	ELSEIF PALAM:COUNT < PALAMLV:1*3
		G = 1
	ELSEIF PALAM:COUNT < PALAMLV:2
		G = 2
	ELSEIF PALAM:COUNT < PALAMLV:2*3
		G = 10
	ELSEIF PALAM:COUNT < PALAMLV:3
		G = 20
	ELSEIF PALAM:COUNT < PALAMLV:3*2
		G = 100
	ELSEIF PALAM:COUNT < PALAMLV:4
		G = 200
	ELSEIF PALAM:COUNT < PALAMLV:5
		G = 1000
	ELSEIF PALAM:COUNT < PALAMLV:6
		G = 2000
	ELSEIF PALAM:COUNT < PALAMLV:7
		G = 3000
	ELSEIF PALAM:COUNT < PALAMLV:8
		G = 5000
	ELSEIF PALAM:COUNT < PALAMLV:9
		G = 8000
	ELSE
		G = 12000
	ENDIF

	IF COUNT == 0
		GOTJUEL:COUNT = G + EX:0 * 1000
	ELSEIF COUNT == 1
		GOTJUEL:COUNT = G + EX:1 * 1000
	ELSEIF COUNT == 2
		GOTJUEL:COUNT = G + EX:2 * 1000
	ELSEIF COUNT < 11
		GOTJUEL:COUNT = G
	ELSE
		GOTJUEL:100 += G
	ENDIF
REND

DRAWLINE
PRINTL 調教の結果：

REPEAT 12
	SIF COUNT == 11
		COUNT = 100
	IF COUNT != 3
		PRINTFORML %PALAMNAME:COUNT%の珠×({JUEL:COUNT}+{GOTJUEL:COUNT})
	ENDIF

	JUEL:COUNT += GOTJUEL:COUNT
REND

PRINTW 以上の珠を得ました。

@DENIAL_CHECK
SIF JUEL:100 == 0
	RETURN 0

$LABEL_1
A = RAND:3 + 4
B = JUEL:100 / 2
SIF B == 0 && JUEL:100 > 0
	B = 1
SIF JUEL:A < B
	B = JUEL:A
JUEL:A -= B
JUEL:100 -= B

SIF B > 0
	PRINTFORML %PALAMNAME:A%の珠×{B}減少
SIF JUEL:100 > 0 && (JUEL:4 + JUEL:5 + JUEL:6) > 0
	GOTO LABEL_1

$LABEL_2
A = RAND:3 + 8
B = JUEL:100 / 2
SIF B == 0 && JUEL:100 > 0
	B = 1
SIF JUEL:A < B
	B = JUEL:A
JUEL:A -= B
JUEL:100 -= B
SIF B > 0
	PRINTFORML %PALAMNAME:A%の珠×{B}個減少
SIF JUEL:100 > 0 && (JUEL:8 + JUEL:9 + JUEL:10) > 0
	GOTO LABEL_2

DRAWLINE
PRINTL 否定の珠と他の珠の打ち消しが発生しています。
PRINTL その結果、

REPEAT 12
	SIF COUNT == 11
		COUNT = 100
	IF COUNT != 3
		PRINTFORML %PALAMNAME:COUNT%の珠×({JUEL:COUNT})
	ENDIF
REND

PRINTW 以上のように変化しました。
