@EVENTSHOP
DRAWLINE


@SHOW_SHOP
DRAWLINE
PRINTV DAY+1
PRINT 日目
IF TIME == 0
	PRINTL  午前
ELSE
	PRINTL  午後
ENDIF
SIF TARGET >= 0
	PRINTFORM %NAME:TARGET%調教中
SIF ASSI >= 0
	PRINTFORM （助手：%NAME:ASSI%）

PRINTFORML (残り${MONEY})

PRINT_ITEM

;キャラカードを持っているか？
C = 0
REPEAT 10
	IF ASSI >= 0
		;助手と同じキャラのカードはだめ
		SIF NO:ASSI == COUNT+1
			CONTINUE
	ENDIF
	N = 50+COUNT
	IF ITEM:N
		C = 1
		BREAK
	ENDIF
REND

DRAWLINE

PRINTS SAVESTR:10

;0-99に自動的に売り物が表示される。
;今回は何も無い
PRINT_SHOPITEM

SIF C || TARGET >= 0
	PRINTL [100] - 調教する
IF TARGET >= 0
	PRINTL [101] - 能力の表示
	PRINTL [102] - 休ませる
ENDIF
PRINTL [200] - セーブ
PRINTL [300] - ロード

@USERSHOP

IF RESULT == 100 && (C || TARGET >= 0)
	IF TARGET < 0
		CALL SELECT_TARGET
	ENDIF

	BEGIN TRAIN
	RETURN 1
ELSEIF RESULT == 101 && TARGET >= 0
	PRINTSL NAME:TARGET
	CALL LIFE_BAR
	CALL SHOW_JUEL
	CALL SHOW_INFO
ELSEIF RESULT == 102 && TARGET >= 0
	;休憩フラグを立てる
	FLAG:0 = 1
	BEGIN TURNEND
	RETURN 1
ELSEIF RESULT == 200
	SAVEGAME
ELSEIF RESULT == 300
	LOADGAME
ENDIF

RETURN 0


;調教するキャラを選ぶ
@SELECT_TARGET
REPEAT 10
	IF ASSI > 0
		;助手と同じキャラのカードはだめ
		SIF NO:ASSI == COUNT+1
			CONTINUE
	ENDIF
	N = 50+COUNT
	IF ITEM:N
		PRINTFORML [{COUNT}]%ITEMNAME:N%
	ENDIF
	N = 60+COUNT
	IF ITEM:N
		PRINTFORML [{COUNT+10}]%ITEMNAME:N%
	ENDIF
REND

$INPUT_LOOP
INPUT
N = 50+RESULT
IF RESULT < 0
	GOTO INPUT_LOOP
ELSEIF RESULT >= 20
	GOTO INPUT_LOOP
ELSEIF ITEM:N == 0
	GOTO INPUT_LOOP
ENDIF

;アイテムひとつ減らす
ITEM:N = ITEM:N-1

IF RESULT < 10
	ADDCHARA RESULT+1
ELSE
	ADDSPCHARA RESULT+1-10
ENDIF

TARGET = CHARANUM-1

;アイテム購入
@EVENTBUY
;調教アイテムは買ったら消える
SIF BOUGHT <= 11
	ITEMSALES:BOUGHT = 0
;キャラカードは買ったら消える
SIF BOUGHT >= 30
	ITEMSALES:BOUGHT = 0


