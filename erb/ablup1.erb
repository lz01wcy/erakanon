@ABLUP1
DRAWLINE
IF ABL:1 >= 5
	PRINTW 既にMAXです。
	RETURN 0
ENDIF

;条件別にＯＫかダメかを記録する
I = 0

IF ABL:1 == 0
	A = 5
ELSEIF ABL:1 == 1
	A = 50
ELSEIF ABL:1 == 2
	A = 1000
ELSEIF ABL:1 == 3
	A = 8000
	;一線越えない
	IF TALENT:27
		TIMES A , 2.00
	ENDIF
ELSEIF ABL:1 == 4
	A = 24000
	;一線越えない
	IF TALENT:27
		TIMES A , 3.00
	ENDIF
ENDIF

;ＬＶ４から５に上げるときは異常経験必要（素質：[快感に素直]なら無視できる）
IF ABL:1 == 4 && TALENT:70 == 0
	PRINTS EXPNAME:50
	PRINTL あり、かつ
	IF EXP:50 == 0
		;異常経験がない
		I |= 2
	ENDIF
ENDIF

;恐怖の珠で上げる
SIF JUEL:5 < A
	I |= 1

PRINT [0] - 
PRINTS PALAMNAME:5
PRINT の珠×
PRINTV A
PRINT ……
IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
	SIF I & 2
		PRINT 経験不足 
ENDIF
PRINTL 

PRINTL [100] - やめる


INPUT
IF (RESULT < 0 || RESULT > 0) && RESULT != 100
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:1 += 1

IF RESULT == 0
	JUEL:5 -= A
ENDIF

PRINTS ABLNAME:1
PRINT のレベルが
PRINTV ABL:1
PRINTW になりました。

CALL YOKUBO_UP_CHECK
