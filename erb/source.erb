@SOURCE_CHECK
;-------------------------------------------------
;バイブなど付けっぱなしアイテムのチェック
;-------------------------------------------------
IF TEQUIP:1
;バイブ装着中
	CALL EQUIP_COM11
ENDIF

IF TEQUIP:3
;アナルバイブ装着中
	CALL EQUIP_COM13
ENDIF

;-------------------------------------------------
;同性のチェック、調教者の能力のチェック
;-------------------------------------------------
CALL SOURCE_SEX_CHECK
CALL PLAYER_SKILL_CHECK

;-------------------------------------------------
;処女喪失のチェック
;-------------------------------------------------
IF EXP:0 && TALENT:0
	PRINTW 処女喪失
	TALENT:0 = 0

	;処女喪失フラグ
	TFLAG:3 = 1
ENDIF

;-------------------------------------------------
;快Ｃのソース
;-------------------------------------------------
;Ｃ敏感とＣ鈍感はここで処理
SIF TALENT:101
	TIMES SOURCE:0 , 0.50
SIF TALENT:102
	TIMES SOURCE:0 , 2.00

A = SOURCE:0

;PALAM:欲情をみる
IF PALAM:0 < PALAMLV:1
	TIMES A , 0.50
ELSEIF PALAM:0 < PALAMLV:2
	TIMES A , 0.70
ELSEIF PALAM:0 < PALAMLV:3
	TIMES A , 1.00
ELSEIF PALAM:0 < PALAMLV:4
	TIMES A , 1.30
ELSEIF PALAM:0 >= PALAMLV:4
	TIMES A , 1.80
ENDIF

B = SOURCE:0

;ABL:欲望をみる
SIF ABL:1 == 0
	TIMES B , 0.10
SIF ABL:1 == 1
	TIMES B , 0.15
SIF ABL:1 == 2
	TIMES B , 0.20
SIF ABL:1 == 3
	TIMES B , 0.25
SIF ABL:1 == 4
	TIMES B , 0.30
SIF ABL:1 == 5
	TIMES B , 0.40

;快感の否定、抑圧、抵抗
IF TALENT:32 || TALENT:34 || TALENT:71
	C = SOURCE:0 / 3
	;ABL:欲望をみる
	SIF ABL:1 == 0
		TIMES C , 1.00
	SIF ABL:1 == 1
		TIMES C , 0.90
	SIF ABL:1 == 2
		TIMES C , 0.70
	SIF ABL:1 == 3
		TIMES C , 0.40
	SIF ABL:1 == 4
		TIMES C , 0.25
	SIF ABL:1 == 5
		TIMES C , 0.10
ELSE
	C = 0
ENDIF

UP:0 += A
UP:5 += B
UP:13 += C

;-------------------------------------------------
;快Ｖのソース
;-------------------------------------------------
;Ｖ敏感とＶ鈍感はここで処理
SIF TALENT:103
	TIMES SOURCE:1 , 0.50
SIF TALENT:104
	TIMES SOURCE:1 , 2.00

A = SOURCE:1

;PALAM:欲情をみる
IF PALAM:0 < PALAMLV:1
	TIMES A , 0.30
ELSEIF PALAM:0 < PALAMLV:2
	TIMES A , 0.50
ELSEIF PALAM:0 < PALAMLV:3
	TIMES A , 1.00
ELSEIF PALAM:0 < PALAMLV:4
	TIMES A , 1.50
ELSEIF PALAM:0 >= PALAMLV:4
	TIMES A , 2.00
ENDIF

B = SOURCE:1

;ABL:欲望をみる
SIF ABL:1 == 0
	TIMES B , 0.10
SIF ABL:1 == 1
	TIMES B , 0.15
SIF ABL:1 == 2
	TIMES B , 0.20
SIF ABL:1 == 3
	TIMES B , 0.25
SIF ABL:1 == 4
	TIMES B , 0.30
SIF ABL:1 == 5
	TIMES B , 0.40

;快感の否定、抑圧、抵抗
IF TALENT:32 || TALENT:34 || TALENT:71
	C = SOURCE:1 / 3
	;ABL:欲望をみる
	SIF ABL:1 == 0
		TIMES C , 1.00
	SIF ABL:1 == 1
		TIMES C , 0.90
	SIF ABL:1 == 2
		TIMES C , 0.70
	SIF ABL:1 == 3
		TIMES C , 0.40
	SIF ABL:1 == 4
		TIMES C , 0.25
	SIF ABL:1 == 5
		TIMES C , 0.10
ELSE
	C = 0
ENDIF

UP:1 += A
UP:4 += B
UP:5 += B
UP:13 += C

;-------------------------------------------------
;快Ａのソース
;-------------------------------------------------
;Ａ敏感とＡ鈍感はここで処理
SIF TALENT:105
	TIMES SOURCE:2 , 0.50
SIF TALENT:106
	TIMES SOURCE:2 , 2.00

A = SOURCE:2

;PALAM:欲情をみる
;快ＡはあまりPALAM:欲情では差が出ない
IF PALAM:0 < PALAMLV:1
	TIMES A , 0.60
ELSEIF PALAM:0 < PALAMLV:2
	TIMES A , 0.80
ELSEIF PALAM:0 < PALAMLV:3
	TIMES A , 1.00
ELSEIF PALAM:0 < PALAMLV:4
	TIMES A , 1.20
ELSEIF PALAM:0 >= PALAMLV:4
	TIMES A , 1.40
ENDIF

B = SOURCE:2

;ABL:欲望をみる
SIF ABL:1 == 0
	TIMES B , 0.05
SIF ABL:1 == 1
	TIMES B , 0.10
SIF ABL:1 == 2
	TIMES B , 0.40
SIF ABL:1 == 3
	TIMES B , 0.80
SIF ABL:1 == 4
	TIMES B , 1.20
SIF ABL:1 == 5
	TIMES B , 1.80

;快感の否定、抑圧、抵抗
IF TALENT:32 || TALENT:34 || TALENT:71
	C = SOURCE:2 / 3
	;ABL:欲望をみる
	SIF ABL:1 == 0
		TIMES C , 1.00
	SIF ABL:1 == 1
		TIMES C , 0.90
	SIF ABL:1 == 2
		TIMES C , 0.70
	SIF ABL:1 == 3
		TIMES C , 0.40
	SIF ABL:1 == 4
		TIMES C , 0.25
	SIF ABL:1 == 5
		TIMES C , 0.10
ELSE
	C = 0
ENDIF

UP:2 += A
UP:6 += B
UP:5 += B
UP:13 += C

;-------------------------------------------------
;同じコマンドの連続実行による上下の処理（快楽系）
;-------------------------------------------------
IF SELECTCOM == PREVCOM
	;UP:0,1,2は絶頂に絡むので先に処理してある
	UP:0 /= 2
	UP:1 /= 2
	UP:2 /= 2
ENDIF

;-------------------------------------------------
;気力０による上下の処理（快楽系）
;-------------------------------------------------
IF BASE:1 <= 0
	;UP:0,1,2は絶頂に絡むので先に処理してある
	UP:0 /= 2
	UP:1 /= 2
	UP:2 /= 2
ENDIF

;-------------------------------------------------
;相性による上下の処理（快楽系）
;-------------------------------------------------
R = NO:PLAYER
IF RELATION:R != 0
	UP:0 *= RELATION:R
	UP:0 /= 100
	UP:1 *= RELATION:R
	UP:1 /= 100
	UP:2 *= RELATION:R
	UP:2 /= 100
ENDIF

;-------------------------------------------------
;素質などによる上下の処理（快楽系）
;-------------------------------------------------
CALL UP_TALENT_CVA_CHECK

;-------------------------------------------------
;愛液処理
;-------------------------------------------------
A = UP:0 + UP:1 + UP:2
;快Ｃ＋快Ｖ＋快Ａが合わせて100以上上昇ならその20%の液体のソースが加わる（オトコはダメ）
IF A > 100 && TALENT:122 == 0
	;濡れやすいと濡れにくいはここで処理
	SIF TALENT:42
		TIMES A , 3.00
	SIF TALENT:43
		TIMES A , 0.40
	SOURCE:10 += A / 5
ENDIF

;-------------------------------------------------
;絶頂処理
;-------------------------------------------------
C = 0
V = 0
A = 0

;絶頂Ｃ
IF UP:0 + PALAM:0 >= PALAMLV:4 * 2
	C = 2
	DOWN:0 = (PALAMLV:4 * 2 - 1000)
	PRINTL 強絶頂Ｃ
ELSEIF UP:0 + PALAM:0 >= PALAMLV:4
	C = 1
	DOWN:0 = (PALAMLV:4 - 1000)
	PRINTL 絶頂Ｃ
ENDIF
;DOWN:0で下げても絶頂以上なら
;その値-1になるように調整（10000で絶頂なら9999）
SIF UP:0 + PALAM:0 - DOWN:0 >= PALAMLV:4
	DOWN:0 = UP:0 + PALAM:0 - PALAMLV:4 + 1

;絶頂Ｖ
IF UP:1 + PALAM:1 >= PALAMLV:4 * 2
	V = 2
	DOWN:1 = (PALAMLV:4 * 2 - 1000)
	PRINTL 強絶頂Ｖ
ELSEIF UP:1 + PALAM:1 >= PALAMLV:4
	V = 1
	DOWN:1 = (PALAMLV:4 - 1000)
	PRINTL 絶頂Ｖ
ENDIF
;DOWN:1で下げても絶頂以上なら
;その値-1になるように調整（10000で絶頂なら9999）
SIF UP:1 + PALAM:1 - DOWN:1 >= PALAMLV:4
	DOWN:1 = UP:1 + PALAM:1 - PALAMLV:4 + 1

;絶頂Ａ
IF UP:2 + PALAM:2 >= PALAMLV:4 * 2
	A = 2
	DOWN:2 = (PALAMLV:4 * 2 - 1000)
	PRINTL 強絶頂Ａ
ELSEIF UP:2 + PALAM:2 >= PALAMLV:4
	A = 1
	DOWN:2 = (PALAMLV:4 - 1000)
	PRINTL 絶頂Ａ
ENDIF
;DOWN:2で下げても絶頂以上なら
;その値-1になるように調整（10000で絶頂なら9999）
SIF UP:2 + PALAM:2 - DOWN:2 >= PALAMLV:4
	DOWN:2 = UP:2 + PALAM:2 - PALAMLV:4 + 1

IF C && V && A
	PRINTL 三 重 絶 頂
	PRINTL (それぞれ4倍の珠が得られます)
	C *= 4
	V *= 4
	A *= 4
ELSEIF C && V
	PRINTL Ｃ＆Ｖ絶頂
	PRINTL (それぞれ2倍の珠が得られます)
	C *= 2
	V *= 2
ELSEIF C && A
	PRINTL Ｃ＆Ａ絶頂
	PRINTL (それぞれ2倍の珠が得られます)
	C *= 2
	A *= 2
ELSEIF V && A
	PRINTL Ｖ＆Ａ絶頂
	PRINTL (それぞれ2倍の珠が得られます)
	V *= 2
	A *= 2
ENDIF
IF C
	SOURCE:12 += 500 * C
	SOURCE:13 += 200 * C
	LOSEBASE:0 += 20
	LOSEBASE:1 += 10

	IF C == 2
		;屈服刻印１に相当
		SIF TFLAG:200 < 1
			TFLAG:200 = 1
	ENDIF
ENDIF
IF V
	SOURCE:12 += 700 * V
	SOURCE:13 += 400 * V
	SOURCE:11 += 800 * V
	SOURCE:16 += 500 * V
	LOSEBASE:0 += 40
	LOSEBASE:1 += 20

	IF V == 1
		;屈服刻印１に相当
		SIF TFLAG:200 < 1
			TFLAG:200 = 1
	ELSEIF V == 2
		;屈服刻印２に相当
		SIF TFLAG:200 < 2
			TFLAG:200 = 2
	ENDIF
ENDIF
IF A
	SOURCE:12 += 1200 * A
	SOURCE:13 += 4500 * A
	SOURCE:11 += 3500 * A
	SOURCE:16 += 1500 * A
	LOSEBASE:0 += 60
	LOSEBASE:1 += 30

	;屈服刻印３に相当
	SIF TFLAG:200 < 3
		TFLAG:200 = 3
ENDIF

;絶頂による欲望ＬＶアップ
L = 0
SIF C
	L = 1
SIF V
	L = 2
SIF A
	L = 3
SIF C && V && L < 2
	L = 2
SIF C >= 4 && A >= 4 && L < 3
	L = 3
SIF V >= 4 && A >= 4 && L < 3
	L = 3
SIF C >= 8 && V >= 8 && A >= 8
	L = 4
;自制心があると１下がる
SIF TALENT:20
	L = L-1

IF ABL:1 < L
	ABL:1 = L
	PRINT そして
	PRINTS ABLNAME:1
	PRINT が
	PRINTV L
	PRINTL になった
	;欲望の上昇による[抑圧][抵抗]の消滅をチェック
	CALL YOKUBO_UP_CHECK
ENDIF

;NOWEXにデータを入れる（絶頂時口上に使う）
NOWEX:0 = C
NOWEX:1 = V
NOWEX:2 = A

;絶頂回数を増やす
EX:0 += C
EX:1 += V
EX:2 += A

;絶頂経験を増やす
EXP:2 += C+V+A

;-------------------------------------------------
;調教対象の射精チェック（ふたなり・オトコ）
;-------------------------------------------------
CALL TARGET_EJAC_CHECK

;-------------------------------------------------
;情愛のソース
;-------------------------------------------------
A = SOURCE:3

;ABL:従順をみる
SIF ABL:0 == 0
	TIMES A , 0.10
SIF ABL:0 == 1
	TIMES A , 0.25
SIF ABL:0 == 2
	TIMES A , 0.40
SIF ABL:0 == 3
	TIMES A , 0.60
SIF ABL:0 == 4
	TIMES A , 0.80
SIF ABL:0 == 5
	TIMES A , 1.00

B = SOURCE:3

SIF ABL:1 == 0
	TIMES B , 0.00
SIF ABL:1 == 1
	TIMES B , 0.05
SIF ABL:1 == 2
	TIMES B , 0.10
SIF ABL:1 == 3
	TIMES B , 0.20
SIF ABL:1 == 4
	TIMES B , 0.30
SIF ABL:1 == 5
	TIMES B , 0.40

UP:4 += A
UP:5 += B

;-------------------------------------------------
;性行動のソース
;-------------------------------------------------
A = SOURCE:4

;ABL:奉仕精神をみる
SIF ABL:6 == 0
	TIMES A , 0.60
SIF ABL:6 == 1
	TIMES A , 0.80
SIF ABL:6 == 2
	TIMES A , 1.00
SIF ABL:6 == 3
	TIMES A , 1.20
SIF ABL:6 == 4
	TIMES A , 1.40
SIF ABL:6 == 5
	TIMES A , 1.70

;抑圧か抵抗がある場合はPALAM:抑鬱が上がる
IF TALENT:32 || TALENT:34
	B = SOURCE:4 / 5
	;ABL:奉仕精神をみる
	SIF ABL:6 == 0
		TIMES B , 2.00
	SIF ABL:6 == 1
		TIMES B , 1.40
	SIF ABL:6 == 2
		TIMES B , 1.00
	SIF ABL:6 == 3
		TIMES B , 0.80
	SIF ABL:6 == 4
		TIMES B , 0.60
	SIF ABL:6 == 5
		TIMES B , 0.40
ELSE
	B = 0
ENDIF

UP:7 += A
UP:13 += B

;-------------------------------------------------
;達成のソース
;-------------------------------------------------
A = SOURCE:5

;ABL:従順をみる
SIF ABL:0 == 0
	TIMES A , 0.50
SIF ABL:0 == 1
	TIMES A , 0.80
SIF ABL:0 == 2
	TIMES A , 1.00
SIF ABL:0 == 3
	TIMES A , 1.20
SIF ABL:0 == 4
	TIMES A , 1.40
SIF ABL:0 == 5
	TIMES A , 1.60

;ABL:奉仕精神をみる
SIF ABL:0 == 0
	TIMES A , 0.00
SIF ABL:0 == 1
	TIMES A , 0.40
SIF ABL:0 == 2
	TIMES A , 0.80
SIF ABL:0 == 3
	TIMES A , 1.20
SIF ABL:0 == 4
	TIMES A , 1.60
SIF ABL:0 == 5
	TIMES A , 2.00

UP:4 += A

;-------------------------------------------------
;痛覚のソース
;-------------------------------------------------
A = SOURCE:6

B = SOURCE:6

;ABL:従順をみる
SIF ABL:0 == 0
	TIMES B , 0.80
SIF ABL:0 == 1
	TIMES B , 0.70
SIF ABL:0 == 2
	TIMES B , 0.55
SIF ABL:0 == 3
	TIMES B , 0.45
SIF ABL:0 == 4
	TIMES B , 0.35
SIF ABL:0 == 5
	TIMES B , 0.25

C = SOURCE:6

SIF ABL:0 == 0
	TIMES C , 0.80
SIF ABL:0 == 1
	TIMES C , 0.60
SIF ABL:0 == 2
	TIMES C , 0.50
SIF ABL:0 == 3
	TIMES C , 0.40
SIF ABL:0 == 4
	TIMES C , 0.20
SIF ABL:0 == 5
	TIMES C , 0.05

D = SOURCE:6

IF ABL:8 == 0
	TIMES C , 1.00
	TIMES D , 0.00
ENDIF
IF ABL:8 == 1
	TIMES C , 0.80
	TIMES D , 0.10
ENDIF
IF ABL:8 == 2
	TIMES C , 0.50
	TIMES D , 0.20
ENDIF
IF ABL:8 == 3
	TIMES C , 0.30
	TIMES D , 0.30
ENDIF
IF ABL:8 == 4
	TIMES C , 0.10
	TIMES D , 0.45
ENDIF
IF ABL:8 == 5
	TIMES C , 0.05
	TIMES D , 0.60
ENDIF

;痛みに弱い
IF TALENT:40
	TIMES D , 4.00
ENDIF

;助手によるプレイ
IF ASSIPLAY
	TIMES C , 0.40
ENDIF

;調教者が【サド】
IF TALENT:PLAYER:83
	TIMES D , 2.50
ENDIF

UP:9 += A
UP:10 += B
UP:11 += C
UP:5 += D
;マゾっ気があるとPALAM:欲情も上がる
;そのとき調教者が【サド】だと効果的

;-------------------------------------------------
;中毒充足のソース
;-------------------------------------------------
A = SOURCE:7
B = SOURCE:7

IF ABL:1 == 0
	TIMES A , 0.10
	TIMES B , 0.20
ENDIF
IF ABL:1 == 1
	TIMES A , 0.15
	TIMES B , 0.30
ENDIF
IF ABL:1 == 2
	TIMES A , 0.20
	TIMES B , 0.40
ENDIF
IF ABL:1 == 3
	TIMES A , 0.25
	TIMES B , 0.50
ENDIF
IF ABL:1 == 4
	TIMES A , 0.30
	TIMES B , 0.60
ENDIF
IF ABL:1 == 5
	TIMES A , 0.35
	TIMES B , 0.70
ENDIF

UP:4 += A
UP:5 += B

;-------------------------------------------------
;不潔のソース
;-------------------------------------------------
A = SOURCE:8
B = SOURCE:8

IF ABL:0 == 0
	TIMES A , 0.60
	TIMES B , 1.00
ENDIF
IF ABL:0 == 1
	TIMES A , 0.40
	TIMES B , 0.80
ENDIF
IF ABL:0 == 2
	TIMES A , 0.25
	TIMES B , 0.60
ENDIF
IF ABL:0 == 3
	TIMES A , 0.10
	TIMES B , 0.30
ENDIF
IF ABL:0 == 4
	TIMES A , 0.00
	TIMES B , 0.10
ENDIF
IF ABL:0 == 5
	TIMES A , 0.00
	TIMES B , 0.00
ENDIF

UP:11 += A
UP:12 += B

;-------------------------------------------------
;液体追加のソース
;-------------------------------------------------
A = SOURCE:10

UP:3 += A

;-------------------------------------------------
;欲情追加のソース
;-------------------------------------------------
A = SOURCE:11

UP:5 += A


;-------------------------------------------------
;露出のソース
;-------------------------------------------------
;恥じらいと恥薄いはここで処理
SIF TALENT:35
	TIMES SOURCE:12 , 2.00
SIF TALENT:36
	TIMES SOURCE:12 , 0.50

;潤滑アップの50％を露出のソースに加える
;ただし液体追加のソースのぶんは引く
SOURCE:12 += (UP:3-SOURCE:10) / 2

A = SOURCE:12

;ABL:露出癖をみる
SIF ABL:7 == 0
	TIMES A , 0.00
SIF ABL:7 == 1
	TIMES A , 0.10
SIF ABL:7 == 2
	TIMES A , 0.20
SIF ABL:7 == 3
	TIMES A , 0.40
SIF ABL:7 == 4
	TIMES A , 0.60
SIF ABL:7 == 5
	TIMES A , 0.80

B = SOURCE:12

;PALAM:恥情をみる
;恥ずかしさを感じれば感じるほどあまり恥ずかしくなくなる
IF PALAM:8 < PALAMLV:1
	TIMES B , 1.00
ELSEIF PALAM:8 < PALAMLV:2
	TIMES B , 0.90
ELSEIF PALAM:8 < PALAMLV:3
	TIMES B , 0.70
ELSEIF PALAM:8 < PALAMLV:4
	TIMES B , 0.50
ELSEIF PALAM:8 >= PALAMLV:4
	TIMES B , 0.30
ENDIF

C = SOURCE:12

;ABL:従順をみる
SIF ABL:0 == 0
	TIMES C , 0.50
SIF ABL:0 == 1
	TIMES C , 0.30
SIF ABL:0 == 2
	TIMES C , 0.15
SIF ABL:0 == 3
	TIMES C , 0.05
SIF ABL:0 == 4
	TIMES C , 0.00
SIF ABL:0 == 5
	TIMES C , 0.00

;ABL:露出癖をみる
SIF ABL:7 == 0
	TIMES C , 1.00
SIF ABL:7 == 1
	TIMES C , 0.90
SIF ABL:7 == 2
	TIMES C , 0.70
SIF ABL:7 == 3
	TIMES C , 0.50
SIF ABL:7 == 4
	TIMES C , 0.30
SIF ABL:7 == 5
	TIMES C , 0.10

UP:5 += A
UP:8 += B

UP:11 += C

;-------------------------------------------------
;屈従のソース
;-------------------------------------------------
A = SOURCE:13
B = SOURCE:13

;ABL:従順をみる
IF ABL:0 == 0
	TIMES A , 0.12
	TIMES B , 0.50
ENDIF
IF ABL:0 == 1
	TIMES A , 0.10
	TIMES B , 0.80
ENDIF
IF ABL:0 == 2
	TIMES A , 0.05
	TIMES B , 1.00
ENDIF
IF ABL:0 == 3
	TIMES A , 0.02
	TIMES B , 1.10
ENDIF
IF ABL:0 == 4
	TIMES A , 0.00
	TIMES B , 1.20
ENDIF
IF ABL:0 == 5
	TIMES A , 0.00
	TIMES B , 1.30
ENDIF

UP:13 += A
UP:6 += B

;-------------------------------------------------
;逸脱のソース
;-------------------------------------------------
;好奇心と保守的はここで処理
SIF TALENT:23
	TIMES SOURCE:14 , 0.30
SIF TALENT:24
	TIMES SOURCE:14 , 3.00

A = SOURCE:14

;ABL:従順をみる
SIF ABL:0 == 0
	TIMES A , 1.00
SIF ABL:0 == 1
	TIMES A , 0.80
SIF ABL:0 == 2
	TIMES A , 0.70
SIF ABL:0 == 3
	TIMES A , 0.40
SIF ABL:0 == 4
	TIMES A , 0.20
SIF ABL:0 == 5
	TIMES A , 0.00

;ABL:欲望をみる
SIF ABL:1 == 0
	TIMES A , 0.90
SIF ABL:1 == 1
	TIMES A , 0.70
SIF ABL:1 == 2
	TIMES A , 0.50
SIF ABL:1 == 3
	TIMES A , 0.30
SIF ABL:1 == 4
	TIMES A , 0.10
SIF ABL:1 == 5
	TIMES A , 0.00

UP:11 += A

;-------------------------------------------------
;反感追加のソース
;-------------------------------------------------
A = SOURCE:15

UP:11 += A

;-------------------------------------------------
;恭順追加のソース
;-------------------------------------------------
A = SOURCE:16

UP:4 += A

;-------------------------------------------------
;調教ソースの表示
;-------------------------------------------------
CALL SHOW_SOURCE

;-------------------------------------------------
;素質などによる上下の処理
;-------------------------------------------------
CALL UP_TALENT_CHECK

;-------------------------------------------------
;相性による上下の処理
;-------------------------------------------------
R = NO:PLAYER
IF RELATION:R != 0
	PRINTFORML ＜相性{RELATION:R/100}.{RELATION:R%100}倍＞

	UP:4 *= RELATION:R
	UP:4 /= 100
	UP:5 *= RELATION:R
	UP:5 /= 100
	UP:7 *= RELATION:R
	UP:7 /= 100
	UP:11 *= 100
	UP:11 /= RELATION:R
	UP:12 *= 100
	UP:12 /= RELATION:R
	UP:13 *= 100
	UP:13 /= RELATION:R
ENDIF

;-------------------------------------------------
;同じコマンドの連続実行による上下の処理
;-------------------------------------------------
IF SELECTCOM == PREVCOM
	;UP:0,1,2は絶頂に絡むので先に処理してある
	PRINTL ＜同一コマンド連続実行＞
	UP:4 /= 2
	UP:5 /= 2
	UP:6 /= 2
	UP:7 /= 2
	UP:8 /= 2
	UP:10 /= 2
ENDIF

;今回のコマンドを「前回のコマンド」とする
PREVCOM = SELECTCOM

;-------------------------------------------------
;気力０による上下の処理
;-------------------------------------------------
IF BASE:1 <= 0
	PRINTL ★気力０★
	;UP:0,1,2は絶頂に絡むので先に処理してある
	UP:3 /= 2
	UP:4 /= 2
	UP:5 /= 2
	UP:7 /= 2
	UP:9 /= 2
	UP:13 /= 2

	LOSEBASE:0 *= 2
	LOSEBASE:0 += 80
ENDIF

;-------------------------------------------------
;苦痛からくる体力・気力の減少
;-------------------------------------------------
LOSEBASE:0 += UP:9 / 16
LOSEBASE:1 += UP:9 / 16

;-------------------------------------------------
;体力・気力の減少
;-------------------------------------------------
BASE:0 -= LOSEBASE:0
BASE:1 -= LOSEBASE:1
PRINT 体力-
PRINTV LOSEBASE:0
PRINT  気力-
PRINTVL LOSEBASE:1

;-------------------------------------------------
;刻印取得のチェック
;-------------------------------------------------
CALL MARK_GOT_CHECK

;-------------------------------------------------
;苦痛快楽経験、奉仕快楽経験のチェック
;-------------------------------------------------
CALL EXP_GOT_CHECK

;-------------------------------------------------
;[即落ち]のチェック
;-------------------------------------------------
CALL SOKUOCHI_CHECK

;-------------------------------------------------
;パラメータの上昇＆表示（DOWNもここで）
;-------------------------------------------------
UPCHECK


;-------------------------------------------------
;調教ソースの表示（DOWNもここで）
;-------------------------------------------------
@SHOW_SOURCE
PRINTL 　
SIF SOURCE:0 > 0
	PRINTFORM 快ｃ({SOURCE:0})
SIF SOURCE:1 > 0
	PRINTFORM 快ｖ({SOURCE:1})
SIF SOURCE:2 > 0
	PRINTFORM 快ａ({SOURCE:2})
SIF SOURCE:3 > 0
	PRINTFORM 情愛({SOURCE:3})
SIF SOURCE:4 > 0
	PRINTFORM 性行動({SOURCE:4})
SIF SOURCE:5 > 0
	PRINTFORM 達成感({SOURCE:5})
SIF SOURCE:6 > 0
	PRINTFORM 痛み({SOURCE:6})
SIF SOURCE:7 > 0
	PRINTFORM 中毒充足({SOURCE:7})
SIF SOURCE:8 > 0
	PRINTFORM 不潔({SOURCE:8})
SIF SOURCE:10 > 0
	PRINTFORM 液体追加({SOURCE:10})
SIF SOURCE:11 > 0
	PRINTFORM 欲情追加({SOURCE:11})
SIF SOURCE:16 > 0
	PRINTFORM 恭順追加({SOURCE:16})
SIF SOURCE:12 > 0
	PRINTFORM 露出({SOURCE:12})
SIF SOURCE:13 > 0
	PRINTFORM 屈従({SOURCE:13})
SIF SOURCE:14 > 0
	PRINTFORM 逸脱({SOURCE:14})
SIF SOURCE:15 > 0
	PRINTFORM 反感追加({SOURCE:15})
PRINTL 　
