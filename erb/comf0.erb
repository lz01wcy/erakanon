@COM0
;愛撫
LOSEBASE:0 += 5
LOSEBASE:1 += 50

SOURCE:4 = 60
SOURCE:8 = 30
SOURCE:12 = 100

;ABL:Ｃ感覚をみる
IF ABL:3 == 0
	SOURCE:0 = 20
	SOURCE:3 = 50
ELSEIF ABL:3 == 1
	SOURCE:0 = 100
	SOURCE:3 = 100
ELSEIF ABL:3 == 2
	SOURCE:0 = 500
	SOURCE:3 = 160
ELSEIF ABL:3 == 3
	SOURCE:0 = 1200
	SOURCE:3 = 200
ELSEIF ABL:3 == 4
	SOURCE:0 = 2000
	SOURCE:3 = 230
ELSE
	SOURCE:0 = 2800
	SOURCE:3 = 250
ENDIF

IF STAIN:0 >= 2 && ASSIPLAY == 0
;奴隷の口の汚れがあるときキスはない（愛液よごれはＯＫ、助手調教時は問題なし）
	SOURCE:8 = 0
	SOURCE:0 /= 2
	SOURCE:3 /= 4
	SOURCE:10 /= 2
ELSE
	;汚臭鈍感
	SIF TALENT:61
		SOURCE:8 /= 4
	;汚臭敏感
	SIF TALENT:62
		SOURCE:8 *= 3
	;自尊心
	SIF TALENT:15
		SOURCE:8 *= 2
	;恋慕
	IF TALENT:85 && ASSIPLAY == 0
		SOURCE:3 *= 2
		SOURCE:8 /= 10
	ENDIF
	IF STAIN:PLAYER:0
	;主人の口の汚れがあると不潔150%
		SOURCE:8 *= 3
		SOURCE:8 /= 2
	ENDIF

	;奴隷の口⇔調教者の口の汚れが移動
	STAIN:0 |= STAIN:PLAYER:0
	STAIN:PLAYER:0 |= STAIN:0
ENDIF

;奴隷のＶ⇔調教者の指の汚れが移動
STAIN:3 |= STAIN:PLAYER:1
STAIN:PLAYER:1 |= STAIN:3

IF TALENT:122 == 0 && TALENT:PLAYER:122 == 0
	PRINTS EXPNAME:40
	PRINTL +5
	EXP:40 += 5
ELSEIF TALENT:122 == 1 && TALENT:PLAYER:122 == 1
	PRINTS EXPNAME:41
	PRINTL +5
	EXP:41 += 5
ENDIF

RETURN 1

