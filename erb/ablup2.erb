@ABLUP2
DRAWLINE
IF ABL:2 >= 5
	PRINTW 既にMAXです。
	RETURN 0
ENDIF

;条件別にＯＫかダメかを記録する
I = 0

IF ABL:2 == 0
	A = 1
ELSEIF ABL:2 == 1
	A = 25
ELSEIF ABL:2 == 2
	A = 200
ELSEIF ABL:2 == 3
	A = 3000
	;一線越えない
	IF TALENT:27
		TIMES A , 2.00
	ENDIF
ELSEIF ABL:2 == 4
	A = 20000
	;一線越えない
	IF TALENT:27
		TIMES A , 3.00
	ENDIF
ENDIF

;自尊心
IF TALENT:15
	TIMES A , 1.50
ENDIF

;習得の珠で上げる
SIF JUEL:7 < A
	I |= 1

PRINT [0] - 
PRINTS PALAMNAME:7
PRINT の珠×
PRINTV A
PRINT ……
IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
	SIF I & 2
		PRINT 経験不足 
ENDIF
PRINTL 

PRINTL [100] - やめる


INPUT
IF (RESULT < 0 || RESULT > 0) && RESULT != 100
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:2 += 1

IF RESULT == 0
	JUEL:7 -= A
ENDIF

PRINTS ABLNAME:2
PRINT のレベルが
PRINTV ABL:2
PRINTW になりました。

