@EVENTFIRST

TARGET = -1

ITEMSALES:0 = 1
ITEMSALES:1 = 1
ITEMSALES:2 = 1
ITEMSALES:3 = 1
ITEMSALES:4 = 1
ITEMSALES:10 = 1
ITEMSALES:11 = 1
ITEMSALES:20 = 1
ITEMSALES:21 = 1
ITEMSALES:50 = 1
ITEMSALES:51 = 1
ITEMSALES:52 = 1
ITEMSALES:53 = 1
ITEMSALES:54 = 1
ITEMSALES:55 = 1
ITEMSALES:56 = 1
ITEMSALES:57 = 1
ITEMSALES:58 = 1
PBAND = 4

MONEY = 500

PRINTFORMW （${MONEY}で調教したいキャラのカードを買ってください）
PRINTW （お金があまればほかのアイテムも買いましょう）

BEGIN SHOP


@EVENTCOMEND
#PRI
IF BASE:0 <= 0
	PRINTFORMSW STR:10
	PRINTFORMSW STR:11
	PRINTFORMSW STR:12
	PRINTFORMSW STR:13
	PRINTFORMSW STR:14
	PRINTFORMSW STR:15
	PRINTFORMSW STR:16

	DRAWLINE
	PRINTL ------------------------------------GAMEOVER-----------------------------------
	DRAWLINE
	WAIT

	QUIT
ELSEIF BASE:0 < 500
	PRINTW （体力が限界に来ています。調教を終了します）
	BEGIN AFTERTRAIN
ENDIF


@EVENTTURNEND
REPEAT CHARANUM
	;体力の回復（午後の調教後は夜の休みが入るので回復大きい）
	IF TIME == 0
		A = MAXBASE:COUNT:0 / 20
	ELSE
		A = MAXBASE:COUNT:0 / 4
	ENDIF
	
	;休憩フラグ（「何もしない」を選択）
	SIF FLAG:0
		A += 100

	;回復早い、回復遅い
	IF TALENT:COUNT:111
		A *= 2
	ELSEIF TALENT:COUNT:112
		A /= 2
	ENDIF

	BASE:COUNT:0 += A
	SIF BASE:COUNT:0 > MAXBASE:COUNT:0
		BASE:COUNT:0 = MAXBASE:COUNT:0

	;気力の回復
	BASE:COUNT:1 = MAXBASE:COUNT:1
REND

FLAG:0 = 0

;午後なら次の日、午前なら午後にする
IF TIME == 1
	DAY = DAY+1
	PRINTW 一日が終わった・・・

	TIME = 0
ELSE
	TIME = 1
ENDIF


BEGIN SHOP

