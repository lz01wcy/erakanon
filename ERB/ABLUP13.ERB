@ABLUP13
DRAWLINE
IF ABL:13 >= 5
	PRINTW 既にMAXです。
	RETURN 0
ENDIF

;条件別にＯＫかダメかを記録する
I = 0
J = 0

IF ABL:13 == 0
	A = 200
	B = 50
	C = 0
	D = 1000
ELSEIF ABL:13 == 1
	A = 1000
	B = 200
	C = 0
	D = 5000
ELSEIF ABL:13 == 2
	A = 3000
	B = 500
	C = 1000
	D = 0
ELSEIF ABL:13 == 3
	A = 8000
	B = 1000
	C = 2000
	D = 0
	;一線越えない
	IF TALENT:27
		TIMES A , 2.00
		TIMES B , 2.00
		TIMES C , 2.00
	ENDIF
ELSEIF ABL:13 == 4
	A = 20000
	B = 2000
	C = 5000
	D = 0
	;一線越えない
	IF TALENT:27
		TIMES A , 3.00
		TIMES B , 3.00
		TIMES C , 3.00
	ENDIF
ENDIF

;倒錯的
IF TALENT:80
	TIMES A , 0.75
	TIMES B , 0.75
	TIMES C , 0.75
	TIMES D , 0.75
ENDIF

;ＬＶ３から４、４から５に上げるときは異常経験必要
IF ABL:13 == 3
	PRINTS EXPNAME:50
	PRINTL あり、かつ
	IF EXP:50 == 0
		;異常経験が不足
		I |= 2
		J |= 2
	ENDIF
ELSEIF ABL:13 == 4
	PRINTS EXPNAME:50
	PRINTL 2以上、かつ
	IF EXP:50 < 2
		;異常経験が不足
		I |= 2
		J |= 2
	ENDIF
ENDIF

;欲情の珠で上げる
SIF JUEL:5 < A
	I |= 1
;充実の珠で上げる
SIF JUEL:6 < C
	I |= 1
;BL経験が必要
SIF EXP:41 < B
	I |= 2

PRINT [0] - 
PRINTS PALAMNAME:5
PRINT の珠×
PRINTV A
IF C > 0
	PRINT 、
	PRINTS PALAMNAME:6
	PRINT の珠×
	PRINTV C
ENDIF
PRINT 、
PRINTS EXPNAME:41
PRINTV B
PRINT 以上
PRINT ……

IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
	SIF I & 2
		PRINT 経験不足 
	SIF I & 4
		PRINT 能力不足 
ENDIF
PRINTL 

IF D > 0
	;快Ｃの珠で上げる
	SIF JUEL:0 < D
		J |= 1
	;レズ経験が必要
	SIF EXP:41 < B
		J |= 2

	PRINT [1] - 
	PRINTS PALAMNAME:0
	PRINT の珠×
	PRINTV D
	PRINT 、
	PRINTS EXPNAME:41
	PRINTV B
	PRINT 以上
	PRINT ……
	
	IF J == 0
		PRINT ＯＫ
	ELSE
		SIF J & 1
			PRINT 珠不足 
		SIF J & 2
			PRINT 経験不足 
		SIF J & 4
			PRINT 能力不足 
	ENDIF
	PRINTL 
ELSE
	J = 256
ENDIF

PRINTL [100] - やめる


INPUT
IF (RESULT < 0 || RESULT > 1) && RESULT != 100
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF J == 256 && RESULT == 1
	RESTART
ELSEIF J != 0 && RESULT == 1
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:13 += 1

IF RESULT == 0
	JUEL:5 -= A
	JUEL:6 -= C
ELSEIF RESULT == 1
	JUEL:0 -= D
ENDIF

PRINTS ABLNAME:13
PRINT のレベルが
PRINTV ABL:13
PRINTW になりました。

